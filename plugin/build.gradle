buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.hierynomus.license" version "0.16.1"
}

apply plugin: 'groovy'
apply from: file('../version.gradle')
apply from: file('bintray.gradle')
apply from: file('gradle-site.gradle')

group 'com.btkelly'
version gnagPluginVersion

compileJava {
    sourceCompatibility = '1.8'
}

repositories {
    jcenter()
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}

dependencies {
    //Gradle specific classes for plugin creation
    implementation gradleApi()

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.github.stkent:githubdiffparser:1.0.1'
    implementation 'org.jetbrains:annotations:21.0.1'
    implementation 'com.vladsch.flexmark:flexmark-all:0.62.2'

    // Built in checks
    implementation('com.puppycrawl.tools:checkstyle:8.45') {
        // See discussion here: https://github.com/btkelly/gnag/issues/196
        exclude group: 'net.sf.saxon', module: 'Saxon-HE'
    }

    implementation 'com.google.code.findbugs:findbugs:3.0.1'
    implementation 'net.sourceforge.pmd:pmd-java:6.37.0'

    // Testing
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

license {
    header = file('../LICENSE_HEADER.txt')
    strictCheck true
}

clean.dependsOn licenseFormat
